// Breath of Fire IV Markup Grammar
%import common.INT -> INT
%import common.DIGIT -> DIGIT
%import common.HEXDIGIT -> HEXDIGIT
%import common.CNAME -> CNAME
%import common.NEWLINE -> NEWLINE
%import common.LETTER -> LETTER
%import common.ESCAPED_STRING -> ESCAPED_STRING 

start: variables? content
variables: variable*
variable: "%" CNAME  /\s*=\s*/ variable_value NEWLINE?
variable_value: variable_int_value -> int_var | ESCAPED_STRING -> string_var
variable_int_value: INT -> number | HEXINT -> hex_number
HEXINT: "0x" HEXDIGIT*

safe_chars: LETTER | DIGIT  | SPACE  | PUNCT 
SPACE: " "
PUNCT: "!" | "\"" | "%" | "%" | "'" | "(" | ")" | "+" | "," | "-" | "." | "/" | ":" | ";" | "?"
content: (content_string)*
content_string: "^" string /\$\s*/
string: string_text*
string_text: (inline_string|multiline_formatting)*
?inline_string.-1: inline_segment 
    | inline_segment NEWLINE  -> end_newline
    | inline_segment "|" NEWLINE -> end_newbox
    | inline_segment "\\" NEWLINE -> end_null
    | inline_segment "*" NEWLINE -> end_asterisk // not sure what this actually does
?inline_segment: inline_formatting
?inline_formatting: inline_formatting inline_color inline_formatting_child 
    | inline_formatting inline_effect inline_formatting_child 
    | inline_formatting_child
inline_color: color_start inline_segment color_end
color_start: /\[\s*COLOR\s*=\s*/i CNAME /\s*\]\s*/ -> str_color
    | /\[\s*COLOR\s*=\s*/i variable_int_value /\s*\]\s*/ -> int_color
color_end: /\[\s*\/COLOR\s*\]/i
inline_effect: effect_start inline_segment effect_end
effect_start:  /\[\s*EFFECT\s*\]/i
effect_end:  /\[\s*\/EFFECT\s*=\s*/i CNAME /\s*\]\s*/
?inline_formatting_child: inline_formatting_child "_" inline_formatting_grandchild -> pause
    | inline_formatting_child "@" inline_formatting_grandchild -> pointer
    | inline_formatting_child macro_block inline_formatting_grandchild -> inline_macro
    | inline_formatting_grandchild
macro_block: /\[\s*/ macro_name /\s*\]/
macro_name: textbox_macro -> textbox_start
    | party_macro  -> party_start
    | selection_macro -> selection_start
    | placeholder_macro -> placeholder_start
    | symbol_macro -> symbol_start
    | longsymbol_macro -> longsymbol_start
    | duration_macro -> duration_start
    | zenny_macro -> zenny_start
    | portrait_macro -> portrait_start
    | inventory_macro -> inventory_start
    | reference_macro -> reference_start
    | typing_macro -> typing_start
textbox_macro: /TB\s+/i CNAME /\s+/ CNAME
party_macro: /PARTY\s+/i CNAME
selection_macro: /SELECTION\s+/i variable_int_value /\s+/ CNAME /\s+/ variable_int_value
placeholder_macro: /PLACEHOLDER\s+/i variable_int_value
symbol_macro: /SYMBOL\s+/i  variable_int_value
longsymbol_macro: /SYMBOL2\s+/i  variable_int_value
duration_macro: /DUR\s+/i variable_int_value
zenny_macro: /ZENNY\s+/i CNAME
portrait_macro: /POR\s+/i CNAME /\s+/ variable_int_value /\s+/ CNAME /\s+/  variable_int_value -> indexed_portrait
    | /POR\s+CLEAR/i -> portrait_clear
inventory_macro: /INV\s+/i variable_int_value /\s+/ CNAME 
reference_macro: /REF\s+/i variable_int_value /\s+/ variable_int_value
typing_macro: /TYPE\s+/i CNAME
?inline_formatting_grandchild: safe_text
safe_text: safe_chars*

multiline_formatting: multiline_color | multiline_effect
multiline_color: color_start inline_string* color_end
multiline_effect: effect_start inline_string* effect_end